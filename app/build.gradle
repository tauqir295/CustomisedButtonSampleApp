plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.sample.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
    }

    /**
     * Adding product flavors - this is useful for different entities with same codebase app
     */
    flavorDimensions "environment", "region"

    productFlavors {

        europe {
            dimension 'region'
            addManifestPlaceholders([APP_NAME:"Sample app europe"])
            addManifestPlaceholders([APP_ICON:"@mipmap/ic_launcher"])
            applicationId 'com.europe.sample.app'
            versionCode 1
            versionName "1.0"
        }

        asia {
            dimension 'region'
            addManifestPlaceholders([APP_NAME:"Sample app asia"])
            addManifestPlaceholders([APP_ICON:"@mipmap/ic_launcher"])
            applicationId 'com.asia.sample.app'
            versionCode 1
            versionName "1.0"
        }

        dev {
            dimension 'environment'
        }

        prod {
            dimension 'environment'
        }
    }

    sourceSets {
        dev {
            assets.srcDirs = ['src/main/assets']
        }

        prod {
            assets.srcDirs = ['src/main/assets']
        }
    }

    String [] excludeVariants = [
            'devEuropeRelease', 'devAsiaRelease'
    ]

    android.variantFilter{
        variant ->
            excludeVariants.each {
                if (variant.getName().contains(it))
                    variant.setIgnore(true)
            }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //mobile design library
    implementation project(path: ':mobile-design-library')
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    //hilt for dependency injection
    implementation 'androidx.fragment:fragment-ktx:1.3.2'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'androidx.test:rules:1.4.0-alpha05'

    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt 'androidx.hilt:hilt-compiler:1.0.0-beta01'

    // room database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation group: 'commons-codec', name: 'commons-codec', version: '1.10'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.work:work-runtime:2.5.0'
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

}